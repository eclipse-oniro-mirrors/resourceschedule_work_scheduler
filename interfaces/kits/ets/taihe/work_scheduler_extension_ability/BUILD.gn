# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//foundation/resourceschedule/work_scheduler/workScheduler.gni")

copy_taihe_idl("copy_WorkSchedulerExtensionAbility") {
  sources = [ "idl/ohos.WorkSchedulerExtensionAbility.taihe" ]
  deps = [
    "../work_scheduler:copy_workScheduler",
    "../work_scheduler_extension_context:copy_WorkSchedulerExtensionContext",
  ]
}

subsystem_name = "resourceschedule"
part_name = "work_scheduler"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name"

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_WorkSchedulerExtensionAbility" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.WorkSchedulerExtensionAbility.ani.cpp",
    "$taihe_generated_file_path/src/ohos.WorkSchedulerExtensionAbility.abi.c",
  ]
}

taihe_shared_library("work_scheduler_extension_ability_ani") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  taihe_generated_file_path = "$taihe_generated_file_path"
  subsystem_name = "$subsystem_name"
  part_name = "$part_name"

  sources = get_target_outputs(":run_taihe")
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.WorkSchedulerExtensionAbility.impl.cpp",
  ]

  deps = [ ":run_taihe" ]

  external_deps = [
    "ability_base:base",
    "ability_base:want",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
  ]
}

generate_static_abc("work_scheduler_extension_ability_abc") {
  base_url = "$taihe_generated_file_path"
  files =
      [ "$taihe_generated_file_path/@ohos.WorkSchedulerExtensionAbility.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/work_scheduler_extension_ability_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("work_scheduler_extension_ability_etc") {
  source = "$target_out_dir/work_scheduler_extension_ability_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":work_scheduler_extension_ability_abc" ]
}

group("work_scheduler_extension_ability_taihe") {
  deps = [
    ":work_scheduler_extension_ability_ani",
    ":work_scheduler_extension_ability_etc",
  ]
}
